## Formatting and Linting

## Scripts related to formatting code
default_lang := 'all'

# Can lint 'ts', 'md', or 'all'. Defaults to all
[group('lint')]
lint lang=default_lang:
  @just _lint-{{lang}}

# lint and fix issues that can be fixed. pass "unsafe" to also apply unsafe fixes.
[group('lint')]
lint-ts-apply unsafe='':
  cd web; npm run lint-apply{{ if unsafe == "unsafe" { "-unsafe" } else { "" } }}

alias l := lint

@_lint-all:
  -just _lint-md
  -just _lint-ts

[no-exit-message]
_lint-md:
  docker run --rm -v $PWD:/workdir davidanson/markdownlint-cli2:v0.8.1 \
    "**/*.md" \
    "#web/node_modules" \
    "#functions/node_modules"

[no-exit-message]
_lint-ts:
  cd web; npm run lint

# Can format 'ts', 'md', or 'all'. Defaults to all
[group('format')]
fmt lang=default_lang:
  @just _fmt-{{lang}}

alias f := fmt

_fmt-all:
  @just _fmt-ts

[no-exit-message]
_fmt-ts:
  cd web; npm run format
